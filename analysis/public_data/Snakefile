"""
Snakemake file to get the error rate stats from public Element dataset

load modules:

    ml bioinfo-tools snakemake

sbatch command:
    
    sbatch -A ngi2016004 -J public -n 12 -p core -t 5:00:00 --wrap "snakemake -p -j 12 --use-singularity -k --rerun-incomplete" --mail-user phojer@kth.se --mail-type ALL
"""


from pathlib import Path
from snakemake.utils import min_version

min_version("8.20.1")

configfile: "../../snakemake_config.yaml"

public_data = {
    # From GIAB
    # Aviti whole genome sequencing of the GIAB Ashkenazim Jewish family trio NIST reference materials was 
    # performed by Element Biosciences. Standard (~350-400 bp) and long insert (~1000+ bp) libraries were seqeunced on the AVITI instrument using in-development R&D reagents and recipes. Information presented in this README relates to HG002, son in the Ashkenazi Jewish trio.
    # Mapping: BWA-MEMs
    "Element_GIAB_HG002_LongIn": "https://42basepairs.com/download/s3/giab/data/AshkenazimTrio/HG002_NA24385_son/Element_AVITI_20231018/HG002_GRCh38-GIABv3_Element-LngInsert_2X150_55x_20231018.bam",
    "Element_GIAB_HG002_ShortIn": "https://42basepairs.com/download/s3/giab/data/AshkenazimTrio/HG002_NA24385_son/Element_AVITI_20231018/HG002_GRCh38-GIABv3_Element-StdInsert_2X150_81x_20231018.bam",
    # From Element Biosciences, Avidity Manuscript
    # Mapping: Sention-BWA
    "Element_Aditity_HG002": "https://42basepairs.com/download/s3/avidity-manuscript-data/data/sentieon-bwa/20220601_PLT-03_BBS-0174-OBPA__FQD-2x150-35x/deduped.bam",
    # From Platinum Pedigree:
    # Sample: 2188 (NA12878)
    "Element_PlatinumPedigree_NA12878": "https://42basepairs.com/download/s3/platinum-pedigree-data/data/element/mapped/GRCh38/2188-E.GRCh38.merged.sort.bam",
    # From GIAB
    "Element_GIAB_HG008-N-D": "https://42basepairs.com/download/s3/giab/data_somatic/HG008/Liss_lab/Element_AVITI_20240118/HG008-N-D_Element-StdInsert_61x_GRCh38-GIABv3.bam",
    # From Element BioSciences resources
    # Downloaded FASTQs from https://go.elementbiosciences.com/human-whole-genome-sequencing-third-party (JM-L825-HG002)
    # Mapped using nf-core/sarek w. BWA-MEM to GRCh38, see nfcore_sarek_public
    "Element_FS_JM-L825-HG002": "../nfcore_sarek_public/outdir/preprocessing/markduplicates/Element_FS_JM-L825-HG002/Element_FS_JM-L825-HG002.md.cram"
}

reads = ["R1", "R2"]
window = 100
bins = list(range(100,2100, window))

base_dir = Path(config["base_dir"])

fasta = base_dir / config["fasta"]

samtools = config["containers"]["samtools"]
fraguracy = config["fraguracy"]


wildcard_constraints:
    dataset = "|".join(public_data.keys()),
    read = "|".join(reads),


rule all:
    input:
        expand("samtools_stats_public/{dataset}_{read}_stats.txt", read=reads, dataset=public_data.keys()),
        expand("fraguracy/{dataset}-counts.txt", dataset=public_data.keys()),
        expand("samtools_stats_public/{dataset}_insert_size/stats_{read}_{bin}.txt", read=reads, bin=bins, dataset=public_data.keys()),


rule download_chr20:
    input: 
        expand("{dataset}_chr20.bam", dataset=public_data.keys()),


rule chr20_bam:
    output:
        bam = "{dataset}_chr20.bam",
        bai = "{dataset}_chr20.bam.bai"
    params:
        bam = lambda wc: public_data[wc.dataset],
        ref = fasta
    threads: 4
    singularity: samtools
    shell:
        "samtools view"
        " -bh"
        " -@ {threads}"
        " -T {params.ref}"
        " -q 60"
        " -o {output.bam}##idx##{output.bai}"
        " --write-index"
        " {params.bam}"
        " chr20"


rule stats_public:
    input:
        bam = "{dataset}_chr20.bam",
        bai = "{dataset}_chr20.bam.bai"
    output:
        txt = "samtools_stats_public/{dataset}_{read}_stats.txt"
    params:
        flag = lambda wildcards: {
            "R1": 64,  # Read1 
            "R2": 128  # Read2
            }[wildcards.read],
        ref = fasta,
    singularity: samtools
    shell:
        "samtools view -bh -q 60 -f {params.flag} {input.bam}"
        " | "
        "samtools stats -r {params.ref} - > {output.txt}"


rule stats_per_interval:
    input:
        bam = "{dataset}_chr20.bam",
        bai = "{dataset}_chr20.bam.bai"
    output:
        txt = "samtools_stats_public/{dataset}_insert_size/stats_{read}_{bin}.txt"
    params:
        bam = lambda wc: public_data[wc.dataset],
        flag = lambda wildcards: {"R1": 66, "R2": 130}[wildcards.read],
        filter = lambda wildcards: f"(tlen >= {wildcards.bin} && tlen < {int(wildcards.bin) + window})"
                                    " || "
                                    f"(tlen <= -{wildcards.bin} && tlen > -{int(wildcards.bin) + window})",
        ref = fasta
    threads: 2
    singularity: samtools
    shell:
        "samtools view -@ {threads} -bh -T {params.ref} -q 60 -f {params.flag} -e '{params.filter}' {input.bam}"
        " | "
        "samtools stats -r {params.ref} - > {output.txt}"


rule fraguracy:
    input:
        bam = "{dataset}_chr20.bam",
        bai = "{dataset}_chr20.bam.bai"
    output:
        multiext("fraguracy/{dataset}-", "counts.txt", "errors.bed.gz", "indel-errors.bed.gz")
    params:
        fraguracy = fraguracy,
        prefix = lambda wc: f"fraguracy/{wc.dataset}",
        fasta = fasta,
    shell:
        "{params.fraguracy} extract"
        " --fasta {params.fasta}"
        " --output-prefix {params.prefix}"
        " --max-read-length 152"
        " --no-denominator"
        " --bin-size 1"
        " {input.bam}"
        " &> {params.prefix}-extract.log"
