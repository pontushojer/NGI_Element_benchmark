"""
load modules:

    ml bioinfo-tools snakemake

sbatch command:
    
    sbatch -A ngi2016004 -J error_rate -n 12 -p core -t 2:00:00 --wrap "snakemake -p -j 12 --use-singularity -k" --mail-user phojer@kth.se --mail-type ALL
"""
from pathlib import Path
from snakemake.utils import min_version

min_version("8.20.1")

configfile: "../../snakemake_config.yaml"

base_dir = Path(config["base_dir"])

runs=["aviti_hq", "aviti_ngi", "xplus_sns"]
cells = [
    "KMS12BM", 
    "MM1S", 
    "OPM2",
    "REH"
]
cells_no_reh = [c for c in cells if c != "REH"]

reads = ["R1", "R2"]

fasta = str(base_dir / config["fasta"])

# Containers and executables
fraguracy = config["fraguracy"]
samtools = config["containers"]["samtools"]
bedtools_container = config["containers"]["bedtools"]


def get_cram(wildcards):
    run = wildcards.run
    cell = wildcards.cell
    cram = f"../nfcore_sarek_rerun/{run}/outdir/preprocessing/markduplicates/{cell}/{cell}.md.cram",
    crai = f"../nfcore_sarek_rerun/{run}/outdir/preprocessing/markduplicates/{cell}/{cell}.md.cram.crai",
    return {"cram": cram, "crai": crai}


rule all:
    input:
        expand("samtools_stats/{run}/{cell}_{read}_chr20_stats.txt", read=reads, run=runs, cell=cells),
        expand("fraguracy/{run}_{cell}{cell}_{cell}-counts.txt", run=runs, cell=cells),
    

rule stats:
    input:
        unpack(get_cram),
    output:
        txt = "samtools_stats/{run}/{cell}_{read}_chr20_stats.txt"
    singularity: samtools
    params:
        flag = lambda wildcards: {
            "R1": 64,  # Read1 
            "R2": 128  # Read2
            }[wildcards.read],
        ref = fasta
    shell:
        "samtools view -bh -T {params.ref} -q 60 -f {params.flag} {input.cram} chr20"
        " | "
        "samtools stats -r {params.ref} - > {output.txt}"


rule get_chr20_bed:
    output:
        "chr20.bed"
    params:
        fai = str(fasta) + ".fai"
    shell:
        "awk '$1 == \"chr20\" {{OFS=\"\\t\"; print $1, 0, $2}}' {params.fai} > {output}"


rule extract_chr20:
    input:
        unpack(get_cram),
    output:
        bam = "bam/{run}_{cell}_chr20.bam",
        bai = "bam/{run}_{cell}_chr20.bam.bai"
    singularity: samtools
    params:
        ref = fasta
    threads: 2
    shell:
        "samtools view -@ {threads} -bh -T {params.ref} -q 60 {input.cram} chr20 -o {output.bam}"
        " && "
        "samtools index {output.bam}"


rule fraguracy:
    input:
        bam = "bam/{run}_{cell}_chr20.bam",
        bai = "bam/{run}_{cell}_chr20.bam.bai"
    output:
        multiext(
            "fraguracy/{run}_{cell}{cell}_{cell}-", 
            "errors.bed.gz", 
            "indel-errors.bed.gz", 
            "counts.txt",
        )
    params:
        fraguracy = fraguracy,
        fasta = fasta,
        prefix = "fraguracy/{run}_{cell}"
    shell:
        "{params.fraguracy} extract"
        " --fasta {params.fasta}"
        " --output-prefix {params.prefix}"
        " --no-denominator"
        " --bin-size 1"
        " {input.bam}"
        " &> {params.prefix}-extract.log"
