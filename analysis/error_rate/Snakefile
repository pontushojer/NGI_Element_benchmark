"""
load modules:

    ml bioinfo-tools snakemake

sbatch command:
    
    sbatch -A ngi2016004 -J error_rate -n 12 -p core -t 2:00:00 --wrap "snakemake -p -j 12 --use-singularity -k" --mail-user phojer@kth.se --mail-type ALL
"""
runs=["aviti_hq", "aviti_ngi", "xplus_sns"]
cells = [
    "KMS12BM", 
    "MM1S", 
    "OPM2",
    "REH"
]
cells_no_reh = [c for c in cells if c != "REH"]

reads = ["R1", "R2"]

fasta = "/vulpes/proj/ngis/ngi2016004/private/strategic_proj/SR_23_02_Element_vs_Illumina/resources/GRCh38_GIABv3/unbgzipped/genome.fa"

# Containers and executables
fragurancy="/vulpes/proj/ngis/ngi2016004/private/pontus/tools/fraguracy-0.2.4"
samtools = "/vulpes/common/ngi/containers/biocontainers/singularity-samtools-1.19.2--h50ea8bc_0.img"
bedtools_container = "/vulpes/ngi/containers/biocontainers/singularity-bedtools-2.30.0--hc088bd4_0.img"


def get_cram(wildcards):
    run = wildcards.run
    cell = wildcards.cell
    cram = f"../nfcore_sarek_rerun/{run}/outdir/preprocessing/markduplicates/{cell}/{cell}.md.cram",
    crai = f"../nfcore_sarek_rerun/{run}/outdir/preprocessing/markduplicates/{cell}/{cell}.md.cram.crai",
    return {"cram": cram, "crai": crai}


rule all:
    input:
        expand("samtools_stats/{run}/{cell}_{read}_chr20_stats.txt", read=reads, run=runs, cell=cells),
        expand("samtools_stats_filt/{run}/{cell}_{read}_chr20_stats.txt", read=reads, run=runs, cell=cells_no_reh),
        expand("fragurancy/{run}_{cell}{cell}_{cell}-counts.txt", run=runs, cell=cells),
        expand("fragurancy_filt/{run}_{cell}{cell}_{cell}-counts.txt", run=runs, cell=cells_no_reh),


rule stats:
    input:
        unpack(get_cram),
    output:
        txt = "samtools_stats/{run}/{cell}_{read}_chr20_stats.txt"
    singularity: samtools
    params:
        flag = lambda wildcards: {
            "R1": 64,  # Read1 
            "R2": 128  # Read2
            }[wildcards.read],
        ref = fasta
    shell:
        "samtools view -bh -T {params.ref} -q 60 -f {params.flag} {input.cram} chr20"
        " | "
        "samtools stats -r {params.ref} - > {output.txt}"


rule get_chr20_bed:
    output:
        "chr20.bed"
    params:
        fai = fasta + ".fai"
    shell:
        "awk '$1 == \"chr20\" {{OFS=\"\\t\"; print $1, 0, $2}}' {params.fai} > {output}"


rule extract_chr20:
    input:
        unpack(get_cram),
    output:
        bam = "bam/{run}_{cell}_chr20.bam",
        bai = "bam/{run}_{cell}_chr20.bam.bai"
    singularity: samtools
    params:
        ref = fasta
    threads: 2
    shell:
        "samtools view -@ {threads} -bh -T {params.ref} -q 60 {input.cram} chr20 -o {output.bam}"
        " && "
        "samtools index {output.bam}"


rule fragurancy:
    input:
        bam = "bam/{run}_{cell}_chr20.bam",
        bai = "bam/{run}_{cell}_chr20.bam.bai"
    output:
        multiext(
            "fragurancy/{run}_{cell}{cell}_{cell}-", 
            "errors.bed.gz", 
            "indel-errors.bed.gz", 
            "counts.txt",
        )
    params:
        fragurancy = fragurancy,
        fasta = fasta,
        prefix = "fragurancy/{run}_{cell}"
    shell:
        "{params.fragurancy} extract"
        " --fasta {params.fasta}"
        " --output-prefix {params.prefix}"
        " --no-denominator"
        " --bin-size 1"
        " {input.bam}"
        " &> {params.prefix}-extract.log"


rule non_variant_positions:
    input:
        vcf = "../variant_call_benchmarking/deepvariant/pacbio/{cell}_chr20_PASS.vcf.gz",
        bed = "chr20.bed"
    output:
        bed = "non_variant_positions/{cell}_chr20.bed"
    singularity: bedtools_container
    shell:
        "bedtools subtract -a {input.bed} -b {input.vcf} > {output.bed}"


rule stats_non_variant:
    input:
        bam = "bam/{run}_{cell}_chr20.bam",
        bai = "bam/{run}_{cell}_chr20.bam.bai",
        bed = "non_variant_positions/{cell}_chr20.bed"
    output:
        txt = "samtools_stats_filt/{run}/{cell}_{read}_chr20_stats.txt"
    singularity: samtools
    params:
        flag = lambda wildcards: {
            "R1": 64,  # Read1 
            "R2": 128  # Read2
            }[wildcards.read],
        ref = fasta
    shell:
        "samtools view -bh  -T {params.ref} -q 60 -f {params.flag} {input.bam}"
        " | "
        "samtools stats -t {input.bed} -r {params.ref} - > {output.txt}"


rule fragurancy_filt:
    input:
        bam = "bam/{run}_{cell}_chr20.bam",
        bai = "bam/{run}_{cell}_chr20.bam.bai",
        bed = "non_variant_positions/{cell}_chr20.bed"
    output:
        multiext(
            "fragurancy_filt/{run}_{cell}{cell}_{cell}-", 
            "errors.bed.gz", 
            "indel-errors.bed.gz", 
            "counts.txt",
        )
    params:
        fragurancy = fragurancy,
        fasta = fasta,
        prefix = "fragurancy_filt/{run}_{cell}"
    shell:
        "{params.fragurancy} extract"
        " --fasta {params.fasta}"
        " --output-prefix {params.prefix}"
        " --no-denominator"
        " --regions {input.bed}"
        " --bin-size 1"
        " {input.bam}"
        " &> {params.prefix}-extract.log"
