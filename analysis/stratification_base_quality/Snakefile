"""
load modules:

    ml bioinfo-tools snakemake

sbatch command:
    
    sbatch -A ngi2016004 -J g4 -n 24 -p core -t 1:00:00 --wrap "snakemake -p -j 24 --use-singularity -k" --mail-user phojer@kth.se --mail-type ALL
"""

runs = [
    "aviti_hq",
    "aviti_ngi",
    "xplus_sns"
]

cells = [
    "KMS12BM",
    "MM1S",
    "OPM2",
    "REH",
]

directions = ["forward", "reverse"]

# From GIAB references
fasta = "/vulpes/proj/ngis/ngi2016004/private/strategic_proj/SR_23_02_Element_vs_Illumina/resources/GRCh38_GIABv3/unbgzipped/genome.fa"

# From GIAB stratifications v3.5
autosomal_bed = "../../resources/GRCh38@all/XY/GRCh38_AllAutosomes.bed.gz"

# https://pqsfinder.fi.muni.cz/hub/hg38/pqsfinder_hg38.bed
g4_bed = "../../resources/G4/pqsfinder_hg38.sort.bed"

# Container
samtools = "/vulpes/common/ngi/containers/biocontainers/singularity-samtools-1.19.2--h50ea8bc_0.img"
bedtools_container = "/vulpes/ngi/containers/biocontainers/singularity-bedtools-2.30.0--hc088bd4_0.img"


def get_cram(wildcards):
    run = wildcards.run
    cell = wildcards.cell
    cram = f"../nfcore_sarek_rerun/{run}/outdir/preprocessing/markduplicates/{cell}/{cell}.md.cram",
    crai = f"../nfcore_sarek_rerun/{run}/outdir/preprocessing/markduplicates/{cell}/{cell}.md.cram.crai",
    return {"cram": cram, "crai": crai}


flank = 150

wildcard_constraints:
    run = "|".join(runs),
    cell = "|".join(cells),
    direction = "|".join(directions),
    direction2 = "|".join(directions),
    updown = "upstream|downstream"

rule all:
    input:
        expand(
            "quals/{run}_{cell}_g4_{direction}.tsv",
            run=runs,
            cell=cells,
            direction=directions,
        )


rule get_isolated_g4:
    input:
        bed = g4_bed
    output:
        bed = "g4_isolated.bed"
    params:
        genome = fasta + ".fai",
        flank = flank * 2 + 1
    singularity: bedtools_container
    shell:
        "bedtools flank"
        " -i {input.bed}"
        " -g {params.genome}"
        " -b {params.flank}"
        " | "
        "bedtools intersect"
        " -v"
        " -a {input.bed}"
        " -b stdin"
        " | "
        # Filter to chr20
        "awk '$1 == \"chr20\"'"
        " > {output.bed}"

        
rule split_g4_by_strand:
    input:
        bed = "g4_isolated.bed"
    output:
        bed_forward = "g4_forward.bed",
        bed_reverse = "g4_reverse.bed"
    shell:
        "awk '$6 == \"+\"' {input.bed} > {output.bed_forward}"
        " && "
        "awk '$6 == \"-\"' {input.bed} > {output.bed_reverse}"


rule get_overlapping_alignments:
    input:
        unpack(get_cram),
        bed = "g4_isolated.bed"
    output:
        bam = "overlapping/{run}_{cell}_g4.bam",
        bai = "overlapping/{run}_{cell}_g4.bam.bai"
    singularity: samtools
    params:
        fasta = fasta
    threads: 4
    shell:
        "samtools view"
        " -F 4" # Not unmapped
        " -F 2048" # Not supplementary
        " --write-index"
        " -@ {threads}"
        " -bh"
        " -L {input.bed}"
        " -M"
        " -T {params.fasta}"
        " -o {output.bam}##idx##{output.bai}"
        " {input.cram}"


rule split_bam_by_strand:
    input:
        bam = "overlapping/{run}_{cell}_g4.bam"
    output:
        bam = "overlapping/{run}_{cell}_g4_{direction}.bam"
    params:
        flag = lambda wc: "-F 16" if wc.direction == "forward" else "-f 16"
    singularity: samtools
    shell:
        "samtools view"
        " -bh"
        " {params.flag}"
        " {input.bam}"
        " > {output.bam}"


rule slop_g4:
    input:
        bed = "g4_isolated.bed"
    output:
        bed = "g4_isolated_slop.bed"
    singularity: bedtools_container
    params:
        genome = fasta + ".fai",
        flank = flank,
    shell:
        "bedtools slop"
        " -i {input.bed}"
        " -g {params.genome}"
        " -b {params.flank}"
        " > {output.bed}"


rule pileup_overlapping_aligments:
    input:
        bam = "overlapping/{run}_{cell}_g4_{direction}.bam",
        bed = "g4_isolated_slop.bed"
    output:
        bed = "pileup/{run}_{cell}_g4_{direction}.bed"
    singularity: samtools
    params:
        fasta = fasta
    threads: 4
    shell:
        "samtools mpileup"
        " -f {params.fasta}"
        " -l {input.bed}"
        " -o {output.bed}"
        " --no-BAQ"
        " --min-BQ 0"
        " -a"
        " -x" # No overlap removal that modifies the base qualities
        " {input.bam}"


rule get_quals_from_pileup:
    input:
        pileup = "pileup/{run}_{cell}_g4_{direction}.bed",
        bed = "g4_isolated.bed"
    output:
        tsv = "quals/{run}_{cell}_g4_{direction}.tsv"
    params:
        script = "../../scripts/mpileup_quals2.py",
        rev = lambda wc: "-r" if wc.direction == "reverse" else "",
        flank = flank
    shell:
        "python {params.script}"
        " {input.pileup}"
        " {input.bed}"
        " {params.rev}"
        " -f {params.flank}"
        " > {output.tsv}"
